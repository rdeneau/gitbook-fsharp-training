# Concepts

## Currying

### Definition

Consiste Ã  transformer :

* une fonction prenant N paramÃ¨tres\
  `Func<T1, T2, ...Tn, TReturn>` en Câ™¯
* en une chaÃ®ne de N fonctions prenant 1 paramÃ¨tre\
  `Func<T1, Func<Tn, ...Func<Tn, TReturn>>>`

### Application partielle

Appel d'une fonction avec moins d'arguments que son nombre de paramÃ¨tres

* Possible grÃ¢ce Ã  la curryfication
* Renvoie fonction prenant en paramÃ¨tre le reste d'arguments Ã  fournir

```fsharp
// Template function with 2 parameters
let insideTag (tagName: string) (content: string) =
    $"<{tagName}>{content}</{tagName}>"

// Helpers with a single parameter `content`
// `tagName` is fixed by partial application
let emphasize = insideTag "em"     // `tagName` fixed to "em"
let strong    = insideTag "strong" // `tagName` fixed to "strong"

// Equivalent - Ã©lÃ©gant mais + explicite
let em content = insideTag "em" content
```

:warning: **Attention :** l'application partielle impacte la signature :

```fsharp
val insideTag: tagName: string -> content: string -> string
val emphasize: (string -> string)  // ğŸ‘ˆ (1)(2)
val em: content: string -> string
```

1. Perte du nom des paramÃ¨tres restant dans la signature (`content`)
2. Signature d'une valeur de type fonction d'oÃ¹ les parenthÃ¨ses (`(string -> string)`)

### Syntaxe des fonctions Fâ™¯

ParamÃ¨tres sÃ©parÃ©s par des espaces\
â†’ Indique que les fonctions sont curryfiÃ©es\
â†’ D'oÃ¹ les `->` dans la signature entre les paramÃ¨tres

```fsharp
let fn () = result         // unit -> TResult
let fn arg = ()            // T    -> unit
let fn arg = result        // T    -> TResult

let fn x y = (x, y)        // T1 -> T2 -> (T1 * T2)

// Equivalents, explicitement curryfiÃ©s :
let fn x = fun y -> (x, y) // 1. Avec une lambda
let fn x =                 // 2. Avec une sous-fonction nommÃ©e
    let fn' y = (x, y)     // N.B. `x` vient du scope
    fn'
```

### IntelliSense Ionide

Dans VsCode avec Ionide, l'IntelliSense fournit un descriptif plus lisible des fonctions, en mettant chaque argument dans une nouvelle ligne :

```fsharp
let pair x y = (x, y)
// val pair:
//    x: 'a ->
//    y: 'b
//    -> 'a * 'b

let triplet x y z = (x, y, z)
// val triplet:
//    x: 'a ->
//    y: 'b ->
//    z: 'c
//    -> 'a * 'b * 'c
```

### Compilation .NET dâ€™une fonction curryfiÃ©e

â˜ Une fonction curryfiÃ©e est compilÃ©e diffÃ©remment selon comment elle est appelÃ©e !

* De base, elle est compilÃ©e en mÃ©thode avec paramÃ¨tres tuplifiÃ©s\
  â†’ Vue comme mÃ©thode normale quand consommÃ©e en Câ™¯

Exemple : Fâ™¯ puis Ã©quivalent Câ™¯ _(version simplifiÃ©e de_ [_SharpLab_](https://sharplab.io/#v2:DYLgZgzgNAJiDUAfAtgexgV2AUwAQEFcBeAWAChdLccAXXAQxhlwA9cBPY13eD8q6tjoA3esAx4iuAEy5EAPgZNcARnJA===)_)_ :

```fsharp
module A =
    let add x y = x + y
    let value = 2 |> add 1
```

```csharp
public static class A
{
    public static int add(int x, int y) => x + y;
    public static int value => 3;
}
```

* Lorsqu'elle est appliquÃ©e partiellement, elle est compilÃ©e sous forme de classe pseudo `Delegate` Ã©tendant `FSharpFunc<int, int>` avec une mÃ©thode `Invoke` qui encapsule les 1ers arguments fournis

Exemple : Fâ™¯ puis Ã©quivalent Câ™¯ _(version simplifiÃ©e de_ [_SharpLab_](https://sharplab.io/#v2:DYLgZgzgNAJiDUAfAtgexgV2AUwAQEFcBeAWAChdLccAXXAQxhlwA9cBPY13eD8q6tjqMYAeQB2eIgya4AjEA===)_)_ :

```fsharp
module A =
    let add x y = x + y
    let addOne = add 1
```

```csharp
public static class A
{
    internal sealed class addOne@3 : FSharpFunc<int, int>
    {
        internal static readonly addOne@3 @_instance = new addOne@3();

        internal addOne@3() { }

        public override int Invoke(int y) => 1 + y;
    }

    public static FSharpFunc<int, int> addOne => addOne@3.@_instance;

    public static int add(int x, int y) => x + y;
}
```

## Conception unifiÃ©e des fonctions

Le type `unit` et la curryfication permettent de concevoir les fonctions simplement comme :

* **Prend un seul paramÃ¨tre** de type quelconque
  * y compris `unit` pour une fonction "sans paramÃ¨tre"
  * y compris une autre fonction _(callback)_
* **Renvoie une seule valeur** de type quelconque
  * y compris `unit` pour une fonction "ne renvoyant rien"
  * y compris une autre fonction

ğŸ‘‰ **Signature universelle** d'une fonction en Fâ™¯ : `'T -> 'U`

## Ordre des paramÃ¨tres

Entre Câ™¯ et Fâ™¯, on ne place pas au mÃªme endroit le paramÃ¨tre concernant l'objet principal (le `this` dans le cas d'une mÃ©thode) :

* Dans mÃ©thode extension Câ™¯, l'objet `this` est le 1er paramÃ¨tre
  * Ex : `items.Select(x => x)`
* En Fâ™¯, l'objet principal est _plutÃ´t_ le **dernier paramÃ¨tre** :\
  \&#xNAN;_(ce qui s'appelle le style data-last)_
  * Ex : `List.map (fun x -> x) items`

Style _data-last_ favorise :

* Pipeline : `items |> List.map square |> List.sum`
* Application partielle : `let sortDesc = List.sortBy (fun i -> -i)`
* Composition de fonctions appliquÃ©es partiellement jusqu'au param "_data_"
  * `(List.map square) >> List.sum`

:warning: Friction avec BCL .NET car plutÃ´t _data-first_

â˜ Solution : wrapper dans une fonction avec params dans ordre sympa en Fâ™¯

```fsharp
let startsWith (prefix: string) (value: string) =
    value.StartsWith(prefix)
```

ğŸ’¡ **Tips** : utiliser `Option.defaultValue` plutÃ´t que `defaultArg` avec les options

* Fonctions font la mÃªme chose mais params `option` et `value` sont inversÃ©s
* `defaultArg option value` : param `option` en 1er ğŸ˜•
* `Option.defaultValue value option` : param `option` en dernier ğŸ‘

De mÃªme, prÃ©fÃ©rer mettre **en 1er** les paramÃ¨tres les + statiques = Ceux susceptibles d'Ãªtre prÃ©dÃ©finis par application partielle

Ex : "dÃ©pendances" qui seraient injectÃ©es dans un objet en Câ™¯

ğŸ‘‰ Application partielle = moyen de simuler l'injection de dÃ©pendances
