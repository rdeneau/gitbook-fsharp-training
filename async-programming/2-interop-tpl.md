---
description: 'TPL : Task Parallel Library'
---

# Interop with .NET TPL

## Interaction with .NET libraries

Asynchronous libraries in .NET and the `async`/`await` C‚ôØ pattern: \
‚Üí Based on **TPL** and the `Task` type

Gateways with asynchronous worflow F‚ôØ :

- `Async.AwaitTask` and `Async.StartAsTask` functions
- `task {}` block

## Gateway functions

`Async.AwaitTask: Task<'T> -> Async<'T>` \
‚Üí Consume an asynchronous .NET library in `async` block

`Async.StartAsTask: Async<'T> -> Task<'T>` \
‚Üí Launch an async calculation as a `Task`

```fsharp
let getValueFromLibrary param = async {
    let! value = DotNetLibrary.GetValueAsync param |> Async.AwaitTask
    return value
}

let computationForCaller param =
    async {
        let! result = getAsyncResult param
        return result
    }
    |> Async.StartAsTask
```

## `task {}` block

> Allows to consume an asynchronous .NET library directly, using a single `Async.AwaitTask` rather than 1 for each async method called.

üí° Available since F‚ôØ 6 _(before, we need [Ply](https://github.com/crowded/ply) package nuget)_

```fsharp
task {
    use client = new System.Net.Http.HttpClient()
    let! response = client.GetStringAsync("https://www.google.fr/")
    response.Substring(0, 300) |> printfn "%s"
}  // Task<unit>
|> Async.AwaitTask  // Async<unit>
|> Async.RunSynchronously
```

## `Async` _vs_ `Task`

#### 1. Calculation start mode

`Task` = _hot tasks_ ‚Üí calculations started immediately‚ùó

`Async` = _task generators_ = calculation specification, independent of startup \
‚Üí Functional approach: no side-effects or mutations, composability \
‚Üí Control of startup mode: when and how üëç

#### 2. Cancellation support

`Task`: by adding a `CancellationToken` parameter to async methods \
‚Üí Forces manual testing if token is canceled = tedious + _error prone‚ùó_

`Async`: automatic support in calculations - token to be provided at startup üëç

## Recommendation for async function in F‚ôØ

C‚ôØ `async` applied at a method level
‚â† F‚ôØ `async` defines an async block, not an async function

‚òù **Recommendation:**
¬ª Put the entire body of the async function in an `async` block.

```fsharp
// ‚ùå Avoid
let workThenWait () =
    Thread.Sleep(1000)
    async { do! Async.Sleep(1000) } // Async only in this block üßê

// ‚úÖ Prefer
let workThenWait () = async {
    Thread.Sleep(1000)
    printfn "work"
    do! Async.Sleep(1000)
}
```

## Pitfalls of the `async`/`await` C‚ôØ pattern

### Pitfall 1 - Really asynchronous?

In C‚ôØ: method `async` remains on the calling thread until the 1st `await`
‚Üí Misleading feeling of being asynchronous throughout the method

```csharp
async Task WorkThenWait() {
    Thread.Sleep(1000); // ‚ö†Ô∏è Blocks calling thread !
    await Task.Delay(1000); // Really async from here ü§î
}
```

### Pitfall 2 - Omit the `await`

```csharp
async Task PrintAfterOneSecond(string message) {
    await Task.Delay(1000);
    Console.WriteLine($"[{DateTime.Now:T}] {message}");
}

async Task Main() {
    PrintAfterOneSecond("Before"); // ‚ö†Ô∏è Missing `await`‚Üí warning CS4014
    Console.WriteLine($"[{DateTime.Now:T}] After");
    await Task.CompletedTask;
}
```

Compiles but returns unexpected result: _After_ before _Before_‚ùó

```txt
[11:45:27] After
[11:45:28] Before
```

This pitfall is also present in F‚ôØ:

```fsharp
let printAfterOneSecond message = async {
    do! Async.Sleep 1000
    printfn $"[{DateTime.Now:T}] {message}"
}

async {
    printAfterOneSecond "Before" // ‚ö†Ô∏è Missing `do!` ‚Üí warning FS0020
    printfn $"[{DateTime.Now:T}] After"
} |> Async.RunSynchronously
```

Compiles but returns another unexpected result: no _Before_ at all ‚ÅâÔ∏è

```txt
[11:45:27] After
```

#### Compilation warnings

The previous examples compile but with big _warnings_!

C‚ôØ [_warning CS4014_](https://docs.microsoft.com/en-us/dotnet/csharp/language-reference/compiler-messages/cs4014) message:

> _Because this call is not awaited, execution of the current method continues before the call is completed._ \
> _Consider applying the `await` operator..._

F‚ôØ _warning FS0020_ message:

> _The result of this expression has type `Async<unit>` and is implicitly ignored._ \
> _Consider using `ignore` to discard this value explicitly..._

‚òù **Recommendation:** be sure to **always** handle this type of _warnings_! _This is even more crucial in F‚ôØ where compilation can be tricky._
