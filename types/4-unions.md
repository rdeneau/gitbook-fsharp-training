---
description: A.k.a. Discriminated Unions (DU)
---

# Unions

## Key points

- Exact term: *Discriminated Union (DU)*
- Sum type: represents an **OR**, a **choice** between several *Cases*
  - Same principle as for an `enum`, but on steroids ğŸ’ª
- Each *case* must have a *Tag* *(a.k.a Label, Discriminator)* -- in `PascalCase` â—
- Each *case* **may** contain data
  - As Tuple: its elements can be named -- in camelCase ğŸ™

```fs
type Ticket =
    | Adult                  // no data -> â‰ƒ singleton stateless
    | Senior of int          // holds an 'int' (w/o more precision)
    | Child of age: int      // holds an 'int' named 'age'
    | Family of Ticket list  // holds a list of tickets
                             // recursive type by default (no 'rec' keyword)
```

### Cases naming

*Cases* can be used without **qualification**: `Int32` *vs* `IntOrBool.Int32`

Qualification can be forced with `RequireQualifiedAccess` attribute: \
â€¢ Cases using common terms (e.g. `None`) â†’ to avoid name collision \
â€¢ Cases names are designed to read better/more explicitly with qualification

*Cases* must be named in **PascalCase** â— \
â€¢ Since F# 7.0, camelCase is allowed for `RequireQualifiedAccess` unions ğŸ’¡

### Field labels

Helpful for:

- Adding meaning to a primitive type \
  â†’ See `Ticket` previous example: `Senior of int` vs `Child of age: int`
- Distinguish between two fields of the same type \
  â†’ See example below:

```fsharp
type ComplexNumber =
    | Cartesian of Real: float * Imaginary: float
    | Polar of Magnitude: float * Phase: float
```

## Instanciation

*Case* â‰ƒ **constructor** \
â†’ Function called with any *case* data

```fs
type Shape =
    | Circle of radius: int
    | Rectangle of width: int * height: int

let circle = Circle 12         // Type: 'Shape', Value: 'Circle 12'
let rect = Rectangle(4, 3)     // Type: 'Shape', Value: 'Rectangle (4, 3)'

let circles = [1..4] |> List.map Circle     // ğŸ‘ˆ Case used as function
```

## Name conflict

When 2 unions have tags with the same name
â†’ Qualify the tag with the union name

```fs
type Shape =
    | Circle of radius: int
    | Rectangle of width: int * height: int

type Draw = Line | Circle    // 'Circle' will conflict with the 'Shape' tag

let draw = Circle            // Type='Draw' (closest type) -- âš ï¸ to be avoided as ambiguous

// Tags qualified by their union type
let shape = Shape.Circle 12
let draw' = Draw.Circle
```

## Unions: get the data out

- Only via *pattern matching*.
- Matching a union type is **exhaustive**.

```fs
type Shape =
    | Circle of radius: float
    | Rectangle of width: float * height: float

let area shape =
    match shape with
    | Circle r -> Math.PI * r * r   // ğŸ’¡ Same syntax as instantiation
    | Rectangle (w, h) -> w * h

let isFlat = function
    | Circle 0.
    | Rectangle (0., _)
    | Rectangle (_, 0.) -> true
    | Circle _
    | Rectangle _ -> false
```

## Single-case unions

Unions with a single case encapsulating a type (usually primitive)

```fs
type CustomerId = CustomerId of int
type OrderId = OrderId of int

let fetchOrder (OrderId orderId) =    // ğŸ’¡ Direct deconstruction without 'match' expression
    ...
```

- **Benefits** ğŸ‘
  - Ensures *type safety* unlike simple type alias <br/> Â Â Â Â  â†’ Impossible to pass a `CustomerId` to a function waiting for an `OrderId`
  - Prevents *Primitive Obsession* at a minimal cost
- **Trap** âš ï¸
  - `OrderId orderId` looks like C# parameter definition

## Enum style unions

All *cases* are empty = devoid of data \
â†’ â‰  .NET `enum` based on numeric values ğŸ“

Instantiation and pattern matching are done just with the *Case*. \
â†’ The *Case* is no longer a ~~function~~ but a *singleton* value.

```fs
type Answer = Yes | No | Maybe
let answer = Yes

let print answer =
    match answer with
    | Yes   -> printfn "Oui"
    | No    -> printfn "Non"
    | Maybe -> printfn "Peut-Ãªtre"
```

ğŸ”— [â€œEnumâ€ style unions | F# for fun and profit](https://fsharpforfunandprofit.com/posts/fsharp-decompiled/#enum-style-unions)

## Unions .Is* properties

The compiler generates `.Is{Case}` properties for each case in a union

- Before Fâ™¯ 9: not accessible + we cannot add them manually ğŸ˜’
- Since Fâ™¯ 9: accessible ğŸ‘

```fsharp
type Contact =
    | Email of address: string
    | Phone of countryCode: int * number: string

type Person = { Name: string; Contact: Contact }

let canSendEmailTo person =  // Person -> bool
    person.Contact.IsEmail   // `.IsEmail` is auto-generated
```

## Union (FP) *vs* Object Hierarchy (OOP)

ğŸ‘‰ A union can usually replace a small *object hierarchy.*

### Explanations

Behaviors/operations implementation:

- **OO:** *virtual methods* in separated classes
- **FP:** *functions* relying on **pattern matchings**
  - exhaustivity
  - avoid duplication by grouping cases
  - improve readability by flattening split cases in a single `match..with`

### FP *vs* OOP

**How we reason about the code** *(at both design and reading time)*

- **FP: by functions** â†’ how an operation is performed for the different cases
- **OOP: by objects** â†’ how all operations are performed for a single case

**Abstraction**

- Objects are more abstract than functions
- Good abstraction is difficult to design
- The more abstract a thing is, the more stable it should be

**ğŸ‘‰ FP is usually easier to write, to understand, to evolve**

### FP *vs* OOP: Open-Closed Principle

It's easier to extend what's **Open.**

**OOP:** open hierarchy, closed operations

- Painful to add an operation: in all classes ğŸ˜“
- Easy to add a class in the hierarchy ğŸ‘

**FP:** open operations, closed cases

- Easy to add an operation ğŸ‘
- Painful to add a case: in all functions ğŸ˜“
  - Still, it's usually easier in Fâ™¯: only 1 file to change

â˜ï¸ **Notes:**

- Adding a class = new concept in the domain â†’ always tricky âš ï¸
- Adding an operation = new behavior for the existing domain concepts
