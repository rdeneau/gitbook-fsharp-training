# Addendum

## Memoization

üí° **Idea:** reduce the calculation time of a function

‚ùì **How to:** cache results\
‚Üí next call with same arguments, return cached result

üëâ **In practice :** function `memoizeN` from library [FSharpPlus](https://fsprojects.github.io/FSharpPlus/reference/fsharpplus-memoization.html#memoizeN)

‚ö†Ô∏è **Caution :** As with any optimization, use it when you need it and check (measure) that it works without any additional inconvenience.

‚òù Not to be confused with `lazy` expression...

## Lazy expression

Syntax sugar to create a .NET `Lazy<'T>` object from an expression

* Expression not evaluated immediately but on 1st request _(_[_Thunk_](https://en.wikipedia.org/wiki/Thunk)_)_
* Interesting for improving performance without overcomplexifying the code

```fsharp
let printAndForward x = printfn $"{x}"; x

let a = lazy (printAndForward "a")

let b = printAndForward "b"
// > b

printfn $"{a.Value} and {b}"
// > a
// > a and b

printfn $"{a.Value} and c"
// > a and c
```

### `Lazy` active pattern

Extracts the value from a `Lazy` object

```fsharp
let triple (Lazy i) = 3 * i // Lazy<int> -> int

let v =
    lazy
        (printf "eval!"
         5 + 5) // Lazy<int>

triple v
// eval!val it: int = 30

triple v
// val it: int = 30
```

## Organizing functions

3 ways to organize functions = 3 places to declare them:

* _Module_: function declared within a module üìç
* _Nested_ : function declared inside a value or inside another function
  * üí° Encapsulate helpers used just locally
  * ‚òù Parent function parameters accessible to function _nested_
* _Method_ : function defined as a method in a type...

## Methods

* Defined with the `member` keyword rather than `let`
* Choice of the _self-identifier_: `this`, `me`, `self`, `_`...
* Choice of the parameters style:
  * Tuplified: OOP style
  * Curried: FP style

### Method Examples

```fsharp
type Product =
    { SKU: string; Price: float }

    // Tuple style, `this`
    member this.TupleTotal(qty, discount) =
        (this.Price * float qty) - discount

    // Curried style, `me`
    member me.CurriedTotal qty discount =
        (me.Price * float qty) - discount
```

## Function _vs_ Method

| Feature                           | Function         | Method                                                |
| --------------------------------- | ---------------- | ----------------------------------------------------- |
| Naming convention                 | camelCase        | PascalCase                                            |
| Currying                          | ‚úÖ yes            | ‚úÖ if not tuplified nor overridden                     |
| Named parameters                  | ‚ùå no             | ‚úÖ if tuplified                                        |
| Optional parameters               | ‚ùå no             | ‚úÖ if tuplified                                        |
| Overload                          | ‚ùå no             | ‚úÖ if tuplified                                        |
| Parameter inference (declaration) | ‚ûñ Possible       | ‚ûñ yes for `this`, possible for the other parameters   |
| Argument inference (usage)        | ‚úÖ yes            | ‚ùå no, object type annotation needed                   |
| High-order function argument      | ‚úÖ yes            | ‚ûñ yes with shorthand member, no with lambda otherwise |
| `inline` supported                | ‚úÖ yes            | ‚úÖ yes                                                 |
| Recursive                         | ‚úÖ yes with `rec` | ‚úÖ yes                                                 |

## Interop with the BCL[^1]

### void method

A .NET `void` method is seen in F# as returning `unit`.

```fsharp
let list = System.Collections.Generic.List<int>()
list.Add
(* IntelliSense Ionide:
abstract member Add:
   item: int
      -> unit
*)
```

Conversely, an F# function returning `unit` is compiled into a `void` method.

```fsharp
let ignore _ = ()
```

Equivalent C# based on [SharpLab](https://sharplab.io/#v2:DYLgZgzgNAJiDUAfYBTALgAgJYHMB2A9gE4oYD6GAvBgBQCUQA==):

```csharp
public static void ignore<T>(T _arg) {}
```

### Calling a BCL method with N arguments

A .NET method with several arguments is "pseudo-tuplified":

* All arguments must be specified (1)
* Partial application of parameters is not supported (2)
* Calls don't work with a real F‚ôØ tuple ‚ö†Ô∏è (3)

```fsharp
System.String.Compare("a", "b") // ‚úÖ (1)
System.String.Compare "a","b"   // ‚ùå
System.String.Compare "a"       // ‚ùå (2)

let tuple = ("a","b")
System.String.Compare tuple     // ‚ùå (3)
```

### `out` Parameter - In C‚ôØ

`out` used to have multiple output values from a method\
‚Üí Ex : `Int32.TryParse`, `Dictionary<,>.TryGetValue` :

```csharp
if (int.TryParse(maybeInt, out var value))
    Console.WriteLine($"It's the number {value}.");
else
    Console.WriteLine($"{maybeInt} is not a number.");
```

### `out` Parameter - In F‚ôØ

Output can be consumed as a tuple üëç

```fsharp
  match System.Int32.TryParse maybeInt with
  | true, i  -> printf $"It's the number {value}."
  | false, _ -> printf $"{maybeInt} is not a number."
```

### Instantiate a class with `new`?

```fsharp
// (1) new allowed but not recommended
type MyClass(i) = class end

let c1 = MyClass(12)      // üëç
let c2 = new MyClass(234) // üëå mais pas idiomatique

// (2) IDisposable => `new` required, `use` replaces `let` (otherwise it's a compiler warning)
open System.IO
let fn () =
    use f = new FileStream("hello.txt", FileMode.Open)
    f.Close()
```

### Calling an overloaded method

* Compiler may not understand which overload is being called
* Tips: call with named argument

```fsharp
let createReader fileName =
    new System.IO.StreamReader(path = fileName)
    // ‚òùÔ∏è Param `path` ‚Üí `filename` inferred as `string`

let createReaderByStream stream =
    new System.IO.StreamReader(stream = stream)
    // ‚òùÔ∏è Param `stream` of type `System.IO.Stream`
```

[^1]: .NET Base Class Library
