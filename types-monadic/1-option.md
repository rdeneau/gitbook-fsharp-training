# Option type

A.k.a `Maybe` *(Haskell),* `Optional` *(Java 8)*

Models the absence of value \
‚Üí In the sense of a possibility for a value to be absent \
‚Üí ‚â† `unit`: used in the case where there is never a value

Defined as a union with 2 *cases* :

```fsharp
type Option<'Value> =
    | None              // Case without data ‚Üí when value is missing
    | Some of 'Value    // Case with data ‚Üí when value is present
```

Common use cases:

- Modeling an optional field
- Turning a partial operation into a total operation

## Modeling an optional field

```fsharp
type Civility = Mr | Mrs
type User = { Name: string; Civility: Civility option }

let joey  = { Name = "Joey"; Civility = Some Mr }
let guest = { Name = "Guest"; Civility = None }
```

‚Üí Make it explicit that `Name` is mandatory and `Civility` optional

‚òù **Warning:** this design does not prevent `Name = null` here *(BCL limit)*

## Partial to total operation

- An operation is **partial** when no output value is possible for certain inputs.
- The operation can become **total** by wrapping the result in an `option`, `None` being used when the operation gives no output.

### Example 1: inverse of a number

```fsharp
let inverse n = 1.0 / n

let tryInverse n =
    match n with
    | 0.0 -> None
    | n   -> Some (1.0 / n)
```

| Function     | Operation | Signature               | `n = 0.5`  | `n = 0.0`    |
|--------------|-----------|-------------------------|------------|--------------|
| `inverse`    | Partial   | `float -> float`        | `2.0`      | `infinity` ‚ùì |
| `tryInverse` | Total     | `float -> float option` | `Some 2.0` | `None` üëå    |

### Example 2: find an element in a collection

- Partial operation: `find predicate` ‚Üí üí• when item not found
- Total operation: `tryFind predicate` ‚Üí `None` or `Some item`

### Benefits üëç

- Explicit, honest / partial operation
  - No special value: `null`, `infinity`
  - No exception
- Forces calling code to handle all cases:
  - `Some value` ‚Üí output value given
  - `None¬†.....` ‚Üí output value missing

## Control flow

How to test for the presence of the value *(of type `'T`)* in the option?

- ‚ùå Do not use `if option.IsSome then ... option.Value` pattern
- ‚úÖ Do *pattern match* the option
- ‚úÖ Do use `Option.xxx` functions

### Control flow with *pattern matching*

Example:

```fsharp
let print option =
    match option with
    | Some x -> printfn "%A" x
    | None   -> printfn "None"

print (Some 1.0)  // 1.0
print None        // None
```

### Control flow with `Option.xxx` helpers

*Mapping* of the inner value (of type `'T`) **if present**: \
‚Üí `map f option` with `f` total operation `'T -> 'U` \
‚Üí `bind f option` with `f` partial operation `'T -> 'U option`

Keep value **if present** and if conditions are met: \
‚Üí `filter predicate option` with `predicate: 'T -> bool` called only if value present

#### Exercise

Implement `map`, `bind` and `filter` with *pattern matching*

<details>

<summary>Solution</summary>

```fsharp
let map f option =             // (f: 'T -> 'U) -> 'T option -> 'U option
    match option with
    | Some x -> Some (f x)
    | None   -> None           // üéÅ 1. Why can't we write `None -> option`?

let bind f option =            // (f: 'T -> 'U option) -> 'T option -> 'U option
    match option with
    | Some x -> f x
    | None   -> None

let filter predicate option =  // (predicate: 'T -> bool) -> 'T option -> 'T option
    match option with
    | Some x when predicate x -> option
    | _ -> None                // üéÅ 2. Implement `filter` with `bind`?
```

<details>

<summary>Bonus questions</summary>

```fsharp
// üéÅ 1. Why can't we write `None -> option`?
let map (f: 'T -> 'U) (option: 'T option) : 'U option =
    match option with
    | Some x -> Some (f x)
    | None   -> (*None*) option  // üí• Type error: `'U option` given != `'T option` expected
```

```fsharp
// üéÅ 2. Implement `filter` with `bind`?
let filter predicate option =  // (predicate: 'T -> bool) -> 'T option -> 'T option
    option |> bind (fun x -> if predicate x then option else None)
```

</details>

</details>

#### Example

```fsharp
// Question/answer console application
type Answer = A | B | C | D

let tryParseAnswer =
    function
    | "A" -> Some A
    | "B" -> Some B
    | "C" -> Some C
    | "D" -> Some D
    | _   -> None

/// Called when the user types the answer on the keyboard
let checkAnswer (expectedAnswer: Answer) (givenAnswer: string) =
    tryParseAnswer givenAnswer
    |> Option.filter ((=) expectedAnswer)
    |> Option.map (fun _ -> "‚úÖ")
    |> Option.defaultValue "‚ùå"

["X"; "A"; "B"] |> List.map (checkAnswer B)  // ["‚ùå"; "‚ùå"; "‚úÖ"]
```

#### Advantages

Makes business logic more readable

- No `if hasValue then / else`
- Highlight the *happy path*
- Handle corner cases at the end

üí° Alternative syntax more light: ad-hoc *computation expressions* üìç

## `Option`: comparison with other types

1. `Option` *vs* `List`
2. `Option` *vs* `Nullable`
3. `Option` *vs* `null`

### `Option` *vs* `List`

Conceptually closed \
‚Üí Option ‚âÉ List of 0 or 1 items \
‚Üí See `Option.toList` function: `'t option -> 't list` (`None -> []`, `Some x -> [x]`)

üí° `Option` & `List` modules: many functions with the same name \
‚Üí `contains`, `count`, `exist`, `filter`, `fold`, `forall`, `map`

‚òù A `List` can have more than 1 element \
‚Üí Type `Option` models absence of value better than type `List`

### `Option` *vs* `Nullable`

`System.Nullable<'T>` ‚âÉ `Option<'T>` but more limited

- ‚ùó Does not work for reference types
- ‚ùó Lacks monadic behavior i.e. `map` and `bind` functions
- ‚ùó Lacks built-in pattern matching `Some x | None`
- ‚ùó In F‚ôØ, no magic as in C‚ôØ / keyword `null`

**Example:**

```fsharp
open System

let x: Nullable<int> = null
// ‚ö†Ô∏è The type 'Nullable<int>' does not have 'null' as a proper value. To create a null value for a Nullable type use 'System.Nullable()' (FS43)

let x' = Nullable<int>()
// val x': Nullable<int> = <null>

let y = Nullable(1)
// val y: Nullable<int> = 1
```

üëâ C‚ôØ uses `Nullable` whereas F‚ôØ uses only `Option`. However, `Nullable` can be required with some libraries, for instance to deal with nullable columns in a database.

### `Option` *vs* `null`

Due to the interop with the BCL, F‚ôØ has to deal with `null` objects in some cases.

üëâ **Good practice**: isolate these cases and wrap them in an `Option` type.

```fsharp
let readLine (reader: System.IO.TextReader) =
    reader.ReadLine() // Can return `null`
    |> Option.ofObj   // `null` becomes None

    // Same than:
    match reader.ReadLine() with
    | null -> None
    | line -> Some line
```

## Nullable reference types

F‚ôØ 9 introduces nullable reference types: a type-safe way to deal with reference types that can have `null` as a valid value.

This feature must be activated:

- Adds `<Nullable>enable</Nullable>` in your `.fsproj`
- Passes `--checknulls+` options to `dotnet fsi` - see `FSharp.FSIExtraInteractiveParameters` settings in vscode

Then, `| null` needs to be added to the type annotation to indicate that `null` as a valid value. It's really similar to nullable reference types: F‚ôØ `string | null` is equivalent to C‚ôØ `string?`, with the usual tradeoff for explicitness over terseness/magic.

```fsharp
let notAValue: string | null = null

let isAValue: string | null = "hello world"

let isNotAValue2: string = null
// Warning FS3261: Nullness warning: The type 'string' does not support 'null'.
```

üîó More details regarding this feature and how F‚ôØ handles nullity (e.g. with `AllowNullLiteral` attribute): [Nullable Reference Types in F# 9](https://devblogs.microsoft.com/dotnet/nullable-reference-types-in-fsharp-9/).
