---
description: CE = Computation Expression
---

# üöÄ CE - Fondements th√©oriques

## CE : le couteau suisse ‚ú®

Les _computation expressions_ servent √† diff√©rentes choses :

* C‚ôØ `yield return` ‚Üí F‚ôØ `seq {}`
* C‚ôØ `async/await` ‚Üí F‚ôØ `async {}`
* C‚ôØ expressions LINQ `from... select` ‚Üí F‚ôØ `query {}`
* ...

Fondements th√©oriques sous-jacents :

* Mono√Øde
* Monade
* Applicative

## Mono√Øde

‚âÉ Type `T` d√©finissant un ensemble comportant :

1. Op√©ration `(+) : T -> T -> T`
   * Pour combiner des ensembles et garder le m√™me "type"
   * Associative : `a + (b + c)` ‚â° `(a + b) + c`
2. √âl√©ment neutre _(aka identity)_ ‚âÉ ensemble vide
   * Combinable √† tout ensemble sans effet
   * `a + e` ‚â° `e + a` ‚â° `a`

### CE mono√Ødale

Le builder d'une CE mono√Ødale _(telle que `seq`)_ dispose _a minima_ de :

* `Yield` pour construire l'ensemble √©l√©ment par √©l√©ment
* `Combine` ‚â° `(+)` (`Seq.append`)
* `Zero` ‚â° √©l√©ment neutre (`Seq.empty`)

S'y ajoute g√©n√©ralement (entre autres) :

* `For` pour supporter `for x in xs do ...`
* `YieldFrom` pour supporter `yield!`

## Monade

‚âÉ Type g√©n√©rique `M<'T>` comportant :

1. Fonction `return` de construction
   * Signature : `(value: 'T) -> M<'T>`
   * ‚âÉ Wrap une valeur
2. Fonction `bind` de "liaison" _(aka op√©rateur `>>=`)_
   * Signature : `(f: 'T -> M<'U>) -> M<'T> -> M<'U>`
   * Utilise la valeur wrapp√©e, la "map" avec la fonction `f`        vers une valeur d'un autre type et "re-wrap" le r√©sultat

### Lois

`return` ‚â° √©l√©ment neutre pour `bind`

* √Ä gauche : `return x |> bind f` ‚â° `f x`
* √Ä droite : `m |> bind return` ‚â° `m`

`bind` est associatif

* `m |> bind f |> bind g` ‚â° `m |> bind (fun x -> f x |> bind g)`

### Langages

**Haskell**

* Monades beaucoup utilis√©es. Les + communes : `IO`, `Maybe`, `State`, `Reader`.
* `Monad` est une _classe de type_ pour cr√©er facilement ses propres monades.

**F‚ôØ**

* Certaines CE permettent des op√©rations monadiques.
* Plus rarement utilis√©es directement _(sauf par des Haskellers)_

**C‚ôØ**

* Monade implicite dans LINQ
* Librairie [LanguageExt](https://github.com/louthy/language-ext) de programmation fonctionnelle

### CE monadique

Le builder d'une CE monadique dispose des m√©thodes `Return` et `Bind`.

Les types `Option` et `Result` sont monadiques. \
‚Üí On peut leur cr√©er leur propre CE :

```fsharp
type OptionBuilder() =
    member _.Bind(x, f) = x |> Option.bind f
    member _.Return(x) = Some x

type ResultBuilder() =
    member _.Bind(x, f) = x |> Result.bind f
    member _.Return(x) = Ok x
```

### CE monadique et g√©n√©rique

[FSharpPlus](http://fsprojects.github.io/FSharpPlus/computation-expressions.html) propose une CE `monad` \
‚Üí Marche pour tous les types monadiques : `Option`, `Result`, ... et m√™me `Lazy` !

```fsharp
#r "nuget: FSharpPlus"
open FSharpPlus

let lazyValue = monad {
    let! a = lazy (printfn "I'm lazy"; 2)
    let! b = lazy (printfn "I'm lazy too"; 10)
    return a + b
} // System.Lazy<int>

let result = lazyValue.Value
// I'm lazy
// I'm lazy too
// val result : int = 12
```

### Exemple avec le type `Option`

```fsharp
#r "nuget: FSharpPlus"
open FSharpPlus

let addOptions x' y' = monad {
    let! x = x'
    let! y = y'
    return x + y
}

let v1 = addOptions (Some 1) (Some 2) // Some 3
let v2 = addOptions (Some 1) None     // None
```

:warning: **Limite :** on ne peut pas m√©langer plusieurs types monadiques !

```fsharp
#r "nuget: FSharpPlus"
open FSharpPlus

let v1 = monad {
    let! a = Ok 2
    let! b = Some 10
    return a + b
} // üí• Error FS0043...

let v2 = monad {
    let! a = Ok 2
    let! b = Some 10 |> Option.toResult
    return a + b
} // val v2 : Result<int,unit> = Ok 12
```

### CE monadiques sp√©cifiques

Librairie [FsToolkit.ErrorHandling](https://github.com/demystifyfp/FsToolkit.ErrorHandling/) propose : \
‚Ä¢ CE `option {}` sp√©cifique au type `Option<'T>` _(exemple ci-dessous)_ \
‚Ä¢ CE `result {}` sp√©cifique au type `Result<'Ok, 'Err>`

‚òù Recommand√© car + explicite que CE `monad`

```fsharp
#r "nuget: FSToolkit.ErrorHandling"
open FsToolkit.ErrorHandling

let addOptions x' y' = option {
    let! x = x'
    let! y = y'
    return x + y
}

let v1 = addOptions (Some 1) (Some 2) // Some 3
let v2 = addOptions (Some 1) None     // None
```

## Applicative

_A.k.a Applicative Functor_

‚âÉ Type g√©n√©rique `M<'T>`

3 "styles" :

**Style A :** Applicatives avec `apply`/`<*>` et `pure`/`return` \
‚Ä¢ ‚ùå Pas facile √† comprendre \
‚Ä¢ üí° Pr√©sent√© par J√©r√©mie Chassaing dans le talk ‚ùù[Applicatives in real life](https://vimeopro.com/newcrafts/newcrafts/video/338449781)‚ùû\
‚Ä¢ ‚òù D√©conseill√© par Don Syme dans cette [note de nov. 2020](https://github.com/dsyme/fsharp-presentations/blob/master/design-notes/rethinking-applicatives.md)

**Style B :** Applicatives avec `mapN` \
‚Ä¢ `map2`, `map3`... `map5` combine 2 √† 5 valeurs wrapp√©es

**Style C :** Applicatives avec `let! ... and! ...` dans une CE \
‚Ä¢ M√™me principe : combiner plusieurs valeurs wrapp√©es \
‚Ä¢ Disponible √† partir de F‚ôØ 5 _(_[_annonce de nov. 2020_](https://devblogs.microsoft.com/dotnet/announcing-f-5/#applicative-computation-expressions)_)_

‚òù **Conseil :** Styles B et C sont autant recommand√©s l'un que l'autre.

### CE applicative

Librairie [FsToolkit.ErrorHandling](https://github.com/demystifyfp/FsToolkit.ErrorHandling/) propose : \
‚Ä¢ Type `Validation<'Ok, 'Err>` ‚â° `Result<'Ok, 'Err list>` \
‚Ä¢ CE `validation {}` supportant syntaxe `let!...and!...`

Permet d'accumuler les erreurs \
‚Üí Usages : \
‚Ä¢ Parsing d'inputs externes \
‚Ä¢ _Smart constructor_ _(Exemple de code slide suivante...)_

```fsharp
#r "nuget: FSToolkit.ErrorHandling"
open FsToolkit.ErrorHandling

type [<Measure>] cm
type Customer = { Name: string; Height: int<cm> }

let validateHeight height =
    if height <= 0<cm>
    then Error "Height must me positive"
    else Ok height

let validateName name =
    if System.String.IsNullOrWhiteSpace name
    then Error "Name can't be empty"
    else Ok name

module Customer =
    let tryCreate name height : Result<Customer, string list> =
        validation {
            let! validName = validateName name
            and! validHeight = validateHeight height
            return { Name = validName; Height = validHeight }
        }

let c1 = Customer.tryCreate "Bob" 180<cm>  // Ok { Name = "Bob"; Height = 180 }
let c2 = Customer.tryCreate "Bob" 0<cm> // Error ["Height must me positive"]
let c3 = Customer.tryCreate "" 0<cm>    // Error ["Name can't be empty"; "Height must me positive"]
```

## Applicative _vs_ Monade

> Soit N op√©rations `tryXxx` renvoyant un `Option` ou `Result`

**Style monadique :**

* Avec `bind` ou CE `let! ... let! ...`
* **Cha√Æne** les op√©rations, ex√©cut√©e 1 √† 1, la N d√©pendant de la N-1
* S'arr√™te √† 1√®re op√©ration KO ‚Üí juste 1√®re erreur dans `Result` ‚ë†
* [_Railway-oriented programming_](https://fsharpforfunandprofit.com/rop/) de Scott Wlaschin

```fsharp
module Result =
    // f : 'T -> Result<'U, 'Err>
    // x': Result<'T, 'Err>
    //  -> Result<'U, 'Err>
    let bind f x' =
        match x' with
        | Error e  -> Error e // üëà (1)
        | Ok value -> f value
```

**Style applicatif :**

* Avec `mapN` ou CE `let! ... and! ...`
* **Combine** 2..N op√©rations ind√©pendantes ‚Üí parall√©lisables üëç
* Permet de combiner les cas `Error` contenant une `List` ‚ë°

```fsharp
module Validation =
    // f : 'T -> 'U -> Result<'V, 'Err list>
    // x': Result<'T, 'Err list>
    // y': Result<'U, 'Err list>
    //  -> Result<'V, 'Err list>
    let map2 f x' y' =
        match x', y' with
        | Ok x, Ok y -> f x y
        | Ok _, Error errors | Error errors, Ok _ -> Error errors
        | Error errors1, Error errors2 -> Error (errors1 @ errors2) // üëà ‚ë°
```

## Autres CE

On a vu 2 librairies qui √©tendent F‚ôØ et proposent leurs CE :

* FSharpPlus ‚Üí `monad`
* FsToolkit.ErrorHandling ‚Üí `option`, `result`, `validation`

Beaucoup de librairies ont leur propre DSL _(Domain Specific Language.)_ Certaines s'appuient alors sur des CE :

* Expecto
* Farmer
* Saturn

### Expecto

‚ùù Librairie de testing : assertions + runner ‚ùû üîó https://github.com/haf/expecto

```fsharp
open Expecto

let tests =
  test "A simple test" {
    let subject = "Hello World"
    Expect.equal subject "Hello World" "The strings should equal"
  }

[<EntryPoint>]
let main args =
  runTestsWithCLIArgs [] args tests
```

### Farmer

‚ùù _Infrastructure-as-code_ pour Azure ‚ùû

üîó [github.com/compositionalit/farmer](https://github.com/compositionalit/farmer)

```fsharp
// Create a storage account with a container
let myStorageAccount = storageAccount {
    name "myTestStorage"
    add_public_container "myContainer"
}

// Create a web app with application insights that's connected to the storage account
let myWebApp = webApp {
    name "myTestWebApp"
    setting "storageKey" myStorageAccount.Key
}

// Create an ARM template (Azure Resource Manager)
let deployment = arm {
    location Location.NorthEurope
    add_resources [
        myStorageAccount
        myWebApp
    ]
}

// Deploy it to Azure!
deployment
|> Writer.quickDeploy "myResourceGroup" Deploy.NoParameters
```

### Saturn

‚ùù Framework Web au-dessus de ASP.NET Core, pattern MVC ‚ùû

üîó [saturnframework.org](https://saturnframework.org/)

```fsharp
open Saturn
open Giraffe

let app = application {
    use_router (text "Hello World from Saturn")
}

run app
```

## Aller + loin

üìπ [Extending F# through Computation Expressions](https://youtu.be/bYor0oBgvws) - üìú [Slides](https://panesofglass.github.io/computation-expressions/#/)

üîó [Computation Expressions Workshop](https://github.com/panesofglass/computation-expressions-workshop)
